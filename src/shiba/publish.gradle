apply plugin: 'maven-publish'
apply plugin: 'com.jfrog.bintray'

def siteUrl = "https://github.com/Tlaster/Shiba"
def gitUrl = siteUrl + ".git"
def issueUrl = siteUrl + "/issues"
def buildNum = System.getenv("TRAVIS_BUILD_NUMBER")
group = "moe.tlaster"
version = "0.3.$buildNum"

publishing {
    publications {
        Production(MavenPublication) {
            artifact("$buildDir/outputs/aar/app-release.aar")
            groupId this.group
            artifactId 'shiba'
            version this.version

            pom.withXml {
                def dependenciesNode = asNode().appendNode('dependencies')

                // Iterate over the implementation dependencies (we don't want the test ones), adding a <dependency> node for each
                configurations.implementation.allDependencies.each {
                    // Ensure dependencies such as fileTree are not included in the pom.
                    if (it.name != 'unspecified') {
                        def dependencyNode = dependenciesNode.appendNode('dependency')
                        dependencyNode.appendNode('groupId', it.group)
                        dependencyNode.appendNode('artifactId', it.name)
                        dependencyNode.appendNode('version', it.version)
                    }
                }
            }
        }
    }
}

bintray {
    user = project.hasProperty('bintrayUser') ?: System.getenv('BINTRAY_USER')
    key = project.hasProperty('bintrayApiKey') ?: System.getenv('BINTRAY_API_KEY')
    publications = ['Production']
    configurations = ['archives']
    override = true
    pkg {
        repo = 'Shiba'
        name = 'Shiba'
        description = "Lightweight Cross-Platform UI framework"
        publish = true
        publicDownloadNumbers = true
        licenses = ['MIT']
        websiteUrl = siteUrl
        vcsUrl = gitUrl
        issueTrackerUrl = issueUrl
        dryRun = true
    }
}
